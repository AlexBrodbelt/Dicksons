/-
Copyright (c) 2025 Alex Brodbelt. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Alex Brodbelt
-/

import Berso.Main
import DicksonsSite.Expanders

-- This gets access to most of the blog genre
open Verso Genre Blog

-- This gets access to Lean code that's in code blocks, elaborated in the same process and
-- environment as Verso
open Verso.Code.External

set_option pp.rawOnError true

-- This is the source of code examples to be shown in the document. It should be relative to the
-- current Lake workspace. One good way to set up the files is in a Git repository that contains one
-- Lake package for example code and another for the docs, as sibling directories.
set_option verso.exampleProject "../Lean"

-- This is the module that will be consulted for example code. It can be overridden using the
-- `(module := ...)` argument to most elements that show code.
set_option verso.exampleModule "Dicksons"

#doc (Page) "Dickson's Classification Theorem" =>

# Overview

This repository provides a formalisation in the Lean 4 theorem prover of results related to
Dickson's classification theorem for finite subgroups of $`\text{SL}(2, F)` and establishes the
connection of this classification to the classification of $`\text{PGL}(2, F)` over an algebraically closed
field $`F`.

The classification of finite subgroups of linear groups is a classical topic in group theory,
with applications to algebraic geometry, representation theory, and number theory.
In particular, the original motivation for this project is contribute towards the formalisation of
Fermat's Last Theorem as can be seen in the blueprint for the ongoing project [FLT](https://imperialcollegelondon.github.io/FLT/blueprint/ch_bestiary.html#a0000000086)


# Mathematical Background

## The Linear Groups and the Projective Linear Groups

The main object of interest in this repository are the four linear groups:

$`\text{GL}(n, F)`,  $`\text{SL}(n, F)`,  $`\text{PGL}(n, F)` and  $`\text{PSL}(n, F)`


### The General and Special Linear Groups

The *general linear group* $`\text{GL}(n, F)` is the group of all invertible $`n \times n` matrices
over the field $`F`, with matrix multiplication as the group operation:

$`\text{GL}(n, F) = \{ A \in M_n(F) \mid \det(A) \neq 0 \}`

The *special linear group* $`\text{SL}(n, F)` is the subgroup of $`\text{GL}(n, F)` consisting of
matrices with determinant equal to 1:

$`\text{SL}(n, F) = \{ A \in \text{GL}(n, F) \mid \det(A) = 1 \}`

### The Projective General and Special Linear Groups

The *projective general linear group* $`\text{PGL}(n, F)` is the quotient of $`\text{GL}(n, F)` by
its center, which consists of scalar matrices $`\lambda I` for $`\lambda \in F^\times`:

$`\text{PGL}(n, F) = \text{GL}(n, F) / Z(\text{GL}(n, F))`

The *projective special linear group* $`\text{PSL}(n, F)` is the quotient of $`\text{SL}(n, F)` by
its center:

$`\text{PSL}(n, F) = \text{SL}(n, F) / Z(\text{SL}(n, F))`

The center of $`\text{SL}(n, F)` consists of scalar matrices $`\lambda I` where $`\lambda^n = 1`,
i.e., the $`n`-th roots of unity in $`F`.

## Reducing the Classification of finite subgroups of PGL‚ÇÇ to the classification of finite subgroups of SL‚ÇÇ

When working over an algebraically closed field it turns out that $`\text{PGL}(n, F)` is isomorphic to
$`\text{PSL}(n, F)` :

```anchor PGL_mulEquiv_PSL (module := Dicksons.Ch4_PGLIsoPSLOverAlgClosedField.ProjectiveGeneralLinearGroup)
noncomputable def PGL_mulEquiv_PSL (n F : Type*) [Fintype n] [DecidableEq n] [Field F]
  [IsAlgClosed F] : PSL n F ‚âÉ* PGL n F :=
    MulEquiv.ofBijective (PSL_monoidHom_PGL n F) (Bijective_PSL_monoidHom_PGL n F)
```

Therefore, considering this isomorphism to classify the finite subgroups of $`\text{PGL}_2(F)` over an algebraically closed field
it suffices to classify the finite subgroups of $`\text{PSL}_2(F)`. Moreover, since the center of the special linear group is precisely

```anchor center_SL2_eq_Z (module := Dicksons.Ch5_PropertiesOfSLOverAlgClosedField.S2_SpecialSubgroups)
lemma center_SL2_eq_Z (R : Type*) [CommRing R]
  [NoZeroDivisors R] : center SL(2,R) = Z R := by
  ext x
  constructor
  ¬∑ intro hx
    rw [SpecialLinearGroup.mem_center_iff] at hx
    obtain ‚ü®z, z_pow_two_eq_one, hz‚ü© := hx
    simp only [Fintype.card_fin, sq_eq_one_iff, scalar_apply, mem_Z_iff] at z_pow_two_eq_one hz ‚ä¢
    rcases z_pow_two_eq_one with (rfl | rfl)
    ¬∑ left
      ext <;> simp [‚Üê hz]
    ¬∑ right
      ext <;> simp [‚Üê hz]
  ¬∑ simp only [mem_Z_iff]
    rintro (rfl | rfl) <;> simp [mem_center_iff]
```

where `Z F` is defined to be the subgroup of $`\text{SL}_2(F)` generated by $`-I`:

```anchor Z (module := Dicksons.Ch5_PropertiesOfSLOverAlgClosedField.S2_SpecialSubgroups)
def Z (R : Type*) [CommRing R] : Subgroup SL(2,R) :=
  closure {(-1 : SL(2,R))}
```

If the characteristic of the ring is equal to $`2`, then the center is isomomorphic to the trivial group
or equivalently, is equal to the bottom subgroup `‚ä• : SL(2,F)`. Therefore, for $`\text{char}(F) = 2` it follows that
 $`\text{PSL}_2(F) \cong \text{SL}_2(F)`.

Otherwise, if $`\text{char}(F) \ne 2` then center contains exactly two elements
and is therefore isomorphic to the cyclic group $`\mathbb{Z}/2\mathbb{Z}`. Thus once finite subgroups of $`\text{SL}_2(F)`
have been classified it should be then straightforward to produce the classification for finite subgroups of $`\text{PSL}_2(F)`.

It is for this reason that the focus is shifted towards classifying the finite subgroups of $`\text{SL}_2(F)`.

The main characters in this classification are three matrices and the subgroups they generate.

## Special Matrices of the Special Linear Group

It turns out that it is sufficient to understand the properties of the following three types of matrices:

### The shear matrix

The shear matrix $`s(\sigma)` for $`\sigma \in F`:

```anchor SpecialMatrices.s (module := Dicksons.Ch5_PropertiesOfSLOverAlgClosedField.S1_SpecialMatrices)
def s (œÉ : F) : SL(2,F) :=
  ‚ü®!![1, 0; œÉ, 1], by simp‚ü©
```

These matrices form an abelian subgroup under matrix multiplication, satisfying the relation
$`s(\sigma) \cdot s(\mu) = s(\sigma + \mu)`:

```anchor SpecialMatrices.s_mul_s_eq_s_add (module := Dicksons.Ch5_PropertiesOfSLOverAlgClosedField.S1_SpecialMatrices)
lemma s_mul_s_eq_s_add (œÉ Œº : F) : s œÉ * s Œº = s (œÉ + Œº) := by
  ext <;> simp [s]
```

### The diagonal matrix

The diagonal matrix $`d(\delta)` for $`\delta \in F^\times`:

```anchor SpecialMatrices.d (module := Dicksons.Ch5_PropertiesOfSLOverAlgClosedField.S1_SpecialMatrices)
def d (Œ¥ : FÀ£) : SL(2, F) :=
  ‚ü®!![(Œ¥ : F), 0; 0 , Œ¥‚Åª¬π], by norm_num‚ü©
```

These matrices form a subgroup isomorphic to $`F^\times`, satisfying the relation
$`d(\delta) \cdot d(\rho) = d(\delta \rho)`:

```anchor SpecialMatrices.d_mul_d_eq_d_mul (module := Dicksons.Ch5_PropertiesOfSLOverAlgClosedField.S1_SpecialMatrices)
lemma d_mul_d_eq_d_mul (Œ¥ œÅ : FÀ£) : d Œ¥ * d œÅ = d (Œ¥ * œÅ) := by
  ext <;> simp [d, mul_comm]
```

### The rotation matrix

The rotation matrix $`w`:

```anchor SpecialMatrices.w (module := Dicksons.Ch5_PropertiesOfSLOverAlgClosedField.S1_SpecialMatrices)
def w : SL(2, F) :=
  ‚ü®!![0,1;-1,0], by norm_num‚ü©
```

which satisfies:

```anchor SpecialMatrices.inv_w_eq_neg_w (module := Dicksons.Ch5_PropertiesOfSLOverAlgClosedField.S1_SpecialMatrices)
lemma inv_w_eq_neg_w {F : Type*} [Field F] :
  (w : SL(2,F))‚Åª¬π  = - w := by ext <;> simp [w]
```

and in particular, $`w^4 = I`.


## Special Subgroups of the Special Linear group


From these matrices it is natural to consider the subgroups of $`\text{SL}_2(F)` generated by these concrete elements.

Later on we will in fact prove that all finite subgroups will be conjugate to one of these subgroups

### The subgroup of shear matrices

The subgroup $`S(F)` consists of all shear matrices:

```anchor SpecialSubgroups.S (module := Dicksons.Ch5_PropertiesOfSLOverAlgClosedField.S2_SpecialSubgroups)
def S (F : Type*) [Field F] : Subgroup SL(2,F) where
  carrier := { s œÉ | œÉ : F }
  mul_mem' := by rintro S Q ‚ü®œÉS, hœÉS‚ü© ‚ü®œÉQ, hœÉQ‚ü©
                 use œÉS + œÉQ; simp [‚Üê hœÉS, ‚Üê hœÉQ]
  one_mem' := ‚ü®0, s_zero_eq_one‚ü©
  inv_mem' := by rintro S ‚ü®œÉ, hœÉ‚ü©
                 use (-œÉ); simp [‚Üê hœÉ]
```

This subgroup is isomorphic to the additive group of the field $`F` (viewed multiplicatively):

```anchor SpecialSubgroups.S_iso_F (module := Dicksons.Ch5_PropertiesOfSLOverAlgClosedField.S2_SpecialSubgroups)
def S_iso_F (F : Type*) [Field F]: S F ‚âÉ* (Multiplicative F) where
  toFun T := T.val 1 0
  invFun œÉ := ‚ü®s œÉ, by use œÉ‚ü©
  left_inv := by
              rintro ‚ü®s, œÉ, hœÉ‚ü©
              ext <;> simp [SpecialMatrices.s, ‚Üê hœÉ]
  right_inv œÉ := by simp [s]
  map_mul' := by
              rintro ‚ü®s‚ÇÅ, œÉ‚ÇÅ, hœÉ‚ÇÅ‚ü© ‚ü®s‚ÇÇ, œÉ‚ÇÇ, hœÉ‚ÇÇ‚ü©
              simp only [Subgroup.coe_mul, Fin.isValue, SpecialLinearGroup.coe_mul, ‚Üê hœÉ‚ÇÅ, ‚Üê hœÉ‚ÇÇ]
              simp [s]
              rfl
```

### The subgroup of diagonal matrices

The subgroup $`D(F)` consists of all diagonal matrices of the form $`d(\delta)`:

```anchor SpecialSubgroups.D (module := Dicksons.Ch5_PropertiesOfSLOverAlgClosedField.S2_SpecialSubgroups)
def D (F : Type*) [Field F] : Subgroup SL(2,F) where
  carrier := { d Œ¥ | Œ¥ : FÀ£ }
  mul_mem' := by rintro S Q ‚ü®Œ¥S, hŒ¥S‚ü© ‚ü®Œ¥Q, hŒ¥Q‚ü©
                 use Œ¥S * Œ¥Q; simp [‚Üê hŒ¥S, ‚Üê hŒ¥Q]
  one_mem' := ‚ü®1, d_one_eq_one‚ü©
  inv_mem' := by rintro S ‚ü®Œ¥, hS‚ü©
                 use Œ¥‚Åª¬π; simp [‚Üê hS]
```

This subgroup is isomorphic to the multiplicative group of units $`F`:

```anchor SpecialSubgroups.D_iso_units (module := Dicksons.Ch5_PropertiesOfSLOverAlgClosedField.S2_SpecialSubgroups)
def D_iso_units (F : Type*) [Field F] : SpecialSubgroups.D F ‚âÉ* FÀ£ where
  toFun d := ‚ü®
              d.val 0 0,
              d.val 1 1,
              by obtain ‚ü®Œ¥, hŒ¥‚ü© := d.property; rw [‚Üê hŒ¥]; simp [SpecialMatrices.d],
              by obtain ‚ü®Œ¥, hŒ¥‚ü© := d.property; rw [‚Üê hŒ¥]; simp [SpecialMatrices.d]
              ‚ü©
  invFun Œ¥ := ‚ü®d Œ¥, by use Œ¥‚ü©
  left_inv := by
              rintro ‚ü®d, Œ¥, hŒ¥‚ü©
              ext <;> simp [SpecialMatrices.d, ‚Üê hŒ¥]
  right_inv a := by ext; rfl
  map_mul' := by
              rintro ‚ü®d‚ÇÅ, Œ¥‚ÇÅ, hŒ¥‚ÇÅ‚ü© ‚ü®d‚ÇÇ, Œ¥‚ÇÇ, hŒ¥‚ÇÇ‚ü©
              congr
              repeat'
                simp_rw [‚Üê hŒ¥‚ÇÅ, ‚Üê hŒ¥‚ÇÇ]
                simp [SpecialMatrices.d, mul_comm]
```

## Elementary Abelian Subgroups

A subgroup $`H` is called elementary abelian of exponent $`p` if it is abelian and every
non-identity element has order $`p`:

```anchor IsElementaryAbelian (module := Dicksons.Ch6_MaximalAbelianSubgroupClassEquation.S1_ElementaryAbelian)
def IsElementaryAbelian {G : Type*} [Group G] (p : ‚Ñï) (H : Subgroup G) : Prop :=
  IsMulCommutative H ‚àß ‚àÄ h : H, h ‚â† 1 ‚Üí orderOf h = p
```

# Dickson's Classification Theorem

## Main Theorem Structure

The classification divides finite subgroups of $`\text{SL}(2, F)` into several cases based on
the relationship between the group and its Sylow $`p`-subgroups, where $`p` is the
characteristic of the field.

## Classification for Characteristic Coprime Groups

When the order of $`G` is coprime to the characteristic $`p` of the field:

```anchor dicksons_classification_theorem_class_I (module := Dicksons.Ch7_DicksonsClassificationTheorem)
theorem dicksons_classification_theorem_class_I {F : Type*} [Field F] [IsAlgClosed F]
  {p : ‚Ñï} [CharP F p] (hp : Prime p) (G : Subgroup (SL(2,F)))  [Finite G]
  (hp' : p = 0 ‚à® Nat.Coprime (Nat.card G) p) :
  IsCyclic G ‚à®
  Isomorphic G (DihedralGroup n)
  ‚à®
  Isomorphic G SL(2, ZMod 3)
  ‚à®
  Isomorphic G SL(2, ZMod 5)
  ‚à®
  Isomorphic G (GL (Fin 2) (ZMod 3))
  := by sorry
```

## Classification for Characteristic Dividing Groups

When the characteristic $`p` divides the order of the group:

```anchor dicksons_classification_theorem_class_II (module := Dicksons.Ch7_DicksonsClassificationTheorem)
theorem dicksons_classification_theorem_class_II {F : Type*} [Field F] [IsAlgClosed F]{p : ‚Ñï}
  [Fact (Nat.Prime p)] [CharP F p] (G : Subgroup (SL(2,F))) [Finite G] (hp : p ‚à£ Nat.card G) :
  ‚àÉ Q : Subgroup SL(2,F), IsElementaryAbelian p Q ‚àß Normal Q ‚àß Isomorphic G Q
  ‚à®
  (p = 2 ‚àß ‚àÉ n : ‚Ñï, Odd n ‚àß Isomorphic G (DihedralGroup n))
  ‚à®
  Isomorphic G SL(2, ZMod 5)
  ‚à®
  ‚àÉ k : ‚Ñï+, Isomorphic G SL(2, GaloisField p k)
  ‚à®
  ‚àÉ k : ‚Ñï+, ‚àÉ x : GaloisField p (2* k), orderOf x^2 = p^(k : ‚Ñï) ‚àß
    ‚àÉ œÜ : G ‚âÉ* SL(2, GaloisField p k), True
  := by sorry
```

## Classification for PGL‚ÇÇ

The classification extends to finite subgroups of $`\text{PGL}(2, \overline{\mathbb{F}_p})`:

```anchor FLT_classification_fin_subgroups_of_PGL2_over_AlgClosure_ZMod (module := Dicksons.Ch7_DicksonsClassificationTheorem)
theorem FLT_classification_fin_subgroups_of_PGL2_over_AlgClosure_ZMod {p : ‚Ñï}
  [Fact (Nat.Prime p)] (ùïÇ : Type*) [Field ùïÇ] [CharP ùïÇ p] [Finite ùïÇ]
  (G : Subgroup (PGL (Fin 2) (AlgebraicClosure (ZMod p)))) [hG : Finite G] :
  IsCyclic G
  ‚à®
  ‚àÉ n, Isomorphic G (DihedralGroup n)
  ‚à®
  Isomorphic G (alternatingGroup (Fin 4))
  ‚à®
  Isomorphic G (Equiv.Perm (Fin 5))
  ‚à®
  Isomorphic G (alternatingGroup (Fin 5))
  ‚à®
  Isomorphic G (PSL (Fin 2) (ùïÇ))
  ‚à®
  Isomorphic G (PGL (Fin 2) (ùïÇ)) := by
    sorry
```

# Repository Structure

The repository is organised as follows:

: `Lean/Dicksons`

  The formal Lean 4 proofs, including:
  - `Ch4_PGLIsoPSLOverAlgClosedField/`: Establishes the isomorphism between PGL and PSL over algebraically closed fields
  - `Ch5_PropertiesOfSLOverAlgClosedField/`: Properties of SL‚ÇÇ including special matrices and subgroups
  - `Ch6_MaximalAbelianSubgroupClassEquation/`: Maximal abelian subgroups and class equations
  - `Ch7_DicksonsClassificationTheorem.lean`: The main classification theorem

: `Verso`

  Lean code for generating this documentation website using the Verso framework.

: `site`

  Jekyll website source files, completed by the Verso-generated content.

# References

- Dickson, L.E. (1901). "Linear Groups with an Exposition of the Galois Field Theory".
  *Teubner*, Leipzig.
- Suzuki, M. (1982). "Group Theory I". *Grundlehren der mathematischen Wissenschaften*,
  vol. 247, Springer-Verlag.
- Huppert, B. (1967). "Endliche Gruppen I". *Grundlehren der mathematischen Wissenschaften*,
  vol. 134, Springer-Verlag.
